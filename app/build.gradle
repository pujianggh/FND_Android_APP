apply plugin: 'com.android.application'

android {
    signingConfigs {
        debugConfig {
            keyAlias 'fengniao123'
            keyPassword 'fengniao123'
            storeFile file('/Users/pujiang/Tools/KEY/fengniao_keystore.jks')
            storePassword 'fengniao123'
        }

        release {
            keyAlias 'fengniao123'
            keyPassword 'fengniao123'
            storeFile file('/Users/pujiang/Tools/KEY/fengniao_keystore.jks')
            storePassword 'fengniao123'
        }
    }
    compileSdkVersion 25
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.fengniao.action"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 20170902
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resConfigs "zh"
        signingConfig signingConfigs.release
        ndk {
            abiFilters "armeabi-v7a","x86","armeabi"  // 指定要ndk需要兼容的架构(这样其他依赖包里mips,x86,armeabi,arm-v8之类的so会被过滤掉)
        }
    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"   // 不显示Log
            //minifyEnabled true                                 // 是否混淆处理
            //zipAlignEnabled true                               // Zipalign优化
            //shrinkResources true                               // 移除无用的resource文件
            signingConfig signingConfigs.release               // 签名配置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 自定义输出配置
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为fengniaodai_v1.0_wandoujia.apk
                        outputFileName = "fengniaodai_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                    }
                }
            }
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"    // 显示Log
            versionNameSuffix "-debug"                         // 生成的版本包名字后缀
            minifyEnabled false                                // 是否混淆处理
            zipAlignEnabled false                              // Zipalign优化
            shrinkResources false                              // 去掉无用资源
            signingConfig signingConfigs.release                 // 签名配置
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
        }
    }

    flavorDimensions "fnd"
    productFlavors {
        QQ {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "QQ"]
        }
        xiaomi {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "xiaomi"]
        }
        oppo {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "oppo"]
        }
        huawei {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "huawei"]
        }
        wandoujia {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "wandoujia"]
        }
        alibaba {
            dimension "fnd"
            manifestPlaceholders = [APP_NAME         : "飞鸟贷",
                                    APP_CHANNEL_VALUE: "alibaba"]
        }
    }

    lintOptions {
        abortOnError false      //错误发生后停止gradle构建
        checkReleaseBuilds true
        xmlReport true
        htmlReport false
        xmlOutput file("$project.buildDir/outputs/lint-result.xml")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    //依赖结合主Module
    implementation project(':commonlibrary')
}
